program ::= (function_definition | comment)*

function_definition ::= 'defn' identifier '::' type docstring? block

docstring ::= string_literal

type ::= 'u8' | 'i8' | 'u16' | 'i16' | 'u32' | 'i32' 
        | 'u64' | 'i64' | 'u128' | 'i128' 
        | 'f32' | 'f64' | 'bool' | 'char' | 'str' | 'void'

block ::= '{' (statement | comment)* '}'

statement ::= return_statement 
            | display_statement 
            | function_call
            | block

return_statement ::= 'return' expression
display_statement ::= 'display' (string_literal | char_literal | identifier)

expression ::= comparison_expression
comparison_expression ::= additive_expression (comparison_operator additive_expression)*
additive_expression ::= multiplicative_expression (additive_operator multiplicative_expression)*
multiplicative_expression ::= primary_expression (multiplicative_operator primary_expression)*

comparison_operator ::= '==' | '!=' | '<' | '<=' | '>' | '>='
additive_operator ::= '+' | '-'
multiplicative_operator ::= '*' | '/'

primary_expression ::= identifier 
          | integer_literal 
          | float_literal 
          | boolean_literal 
          | char_literal 
          | string_literal 
          | type_constant
          | '(' expression ')'

type_constant ::= type '.' ('min' | 'max')

function_call ::= identifier

comment ::= ';' [^\n]*

identifier ::= [a-zA-Z_] [a-zA-Z0-9_?-]*
integer_literal ::= [0-9]+
float_literal ::= [0-9]+ '.' [0-9]+
boolean_literal ::= 'true' | 'false'
char_literal ::= "'" [^'\n] "'"
string_literal ::= '"' [^"\n]* '"'
